buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.wisepersist:gwt-gradle-plugin:${gwtPluginVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'war'
apply plugin: 'gwt'
apply from: rootProject.file('gradle/it.gradle')

sourceCompatibility = 1.8

configurations { providedRuntime }

dependencies {
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-web'){
        exclude module: 'spring-boot-starter-tomcat'
    }
    compile('org.springframework.boot:spring-boot-starter-webflux')

    gwt "com.google.gwt:gwt-user:$gwtVersion"
    gwt "com.google.elemental2:elemental2-core:$elementalVersion"
    gwt "com.google.elemental2:elemental2-dom:$elementalVersion"

    gwtSdk ("com.google.gwt:gwt-dev:$gwtVersion") { 
        exclude module: 'apache-jsp'
    }

    runtime "com.google.gwt:gwt-servlet:$gwtVersion"

    providedRuntime('org.springframework.boot:spring-boot-starter-jetty')
    
    testCompile project(':gwt-spring-boot-sample-common-test')
}

gwt {
    gwtVersion = '' // GWT SDK dependencies will be manually managed 
    modules 'gwt.springboot.sample.sse.Application'

    maxHeapSize = '2g'
    eclipse{ addGwtContainer = false }
    superDev{ launcherDir = file('build/gwt/launcherDir') }
}

bootWar {
    dependsOn compileGwt
    into('static') { from compileGwt.outputs }
}